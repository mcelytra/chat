plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.mcelytra'
version = '1.0.0-SNAPSHOT'

sourceCompatibility = 8

ext.moduleName = 'org.mcelytra.chat'

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://aperlambda.github.io/maven' }
    mavenCentral()
}

dependencies {
    compile 'net.md-5:bungeecord-chat:1.12-SNAPSHOT'
    compile 'org.aperlambda:lambdajcommon:1.7.1'
}

javadoc {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.addStringOption('XDignore.symbol.file')
    }
}

jar {
    from 'LICENSE'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        elytrachat(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = 'elytra-chat'
            version = project.version

            artifact sourcesJar
            artifact javadocJar
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
