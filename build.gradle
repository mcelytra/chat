plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'org.mcelytra'
version = '1.0.0-SNAPSHOT'

ext.moduleName = 'org.mcelytra.chat'

compileJava.options.encoding = 'UTF-8'

repositories {
    maven { url = 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    maven { url = 'https://aperlambda.github.io/maven' }
    mavenCentral()
}

dependencies {
    api 'net.md-5:bungeecord-chat:1.15-SNAPSHOT'
    api 'org.aperlambda:lambdajcommon:1.8.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    inputs.property("moduleName", moduleName)
    doFirst {
        options.addStringOption('XDignore.symbol.file')
    }
}

jar {
    from 'LICENSE'
    inputs.property("moduleName", moduleName)
    manifest {
        attributes('Automatic-Module-Name': moduleName)
    }
}

publishing {
    publications {
        elytrachat(MavenPublication) {
            from components.java
            artifactId = 'elytra-chat'
        }
    }
    repositories {
        maven {
            url "$buildDir/repo"
        }
    }
}
